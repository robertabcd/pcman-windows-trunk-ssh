; Script generated by the HM NIS Edit Script Wizard.
; !define _COMBO_
; HM NIS Edit Wizard helper defines
!ifdef _COMBO_
!define PRODUCT_NAME "PCMan Combo"
!define SRC_DIR "Combo\Release\PCMan Combo"
!define CONFIG_FOLDER "PCMAN Combo"
OutFile ".\Release\PCManCB.exe"
!else
!define PRODUCT_NAME "PCMan"
!define SRC_DIR "Lite\Release\PCMan"
!define CONFIG_FOLDER "PCMAN"
OutFile ".\Release\PCMan.exe"
!endif

; Product definitions
!define PRODUCT_DIR "${PRODUCT_NAME}"
!define PRODUCT_VERSION "Novus"
!define PRODUCT_PUBLISHER "PCMan Team"
!define PRODUCT_WEB_SITE "http://pcman.openfoundry.org/"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\PCMan.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define /date BUILD_TIME "%H:%M %p, %Y-%m-%d"

!include "MUI2.nsh"
!include "nsDialogs.nsh"

; Global definitions
!define SHELLFOLDERS "Software\Microsoft\Windows\CurrentVersion\Explorer\Shell Folders"
SetCompressor /SOLID lzma
Var clean_install	; This is a clean installation without previous configuration files.

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\orange-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\orange-uninstall.ico"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

;BGGradient 0000FF 000000 FFFFFF
;Caption "${PRODUCT_NAME} ${PRODUCT_VERSION} ${BUILD_TIME}"
BrandingText "Copyright (C) 2009 PCMan Team  /  Build Time: ${BUILD_TIME}"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
;!define MUI_LICENSEPAGE_CHECKBOX
!insertmacro MUI_PAGE_LICENSE "License.txt"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!define MUI_FINISHPAGE_NOAUTOCLOSE
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\PCMan.exe"
!define MUI_FINISHPAGE_SHOWREADME_NOTCHECKED
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\changelog.txt"
!define MUI_FINISHPAGE_LINK $(MSG_OPEN_BACKUP_FILES)
!define MUI_FINISHPAGE_LINK_LOCATION "$INSTDIR\Config\backup\"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES


; Language files
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "TradChinese"

; Language strings
LangString SYMBOLS_LNK ${LANG_TRADCHINESE}             "標點符號輸入程式.lnk"
LangString SYMBOLS_LNK ${LANG_ENGLISH}                 "Symbols.lnk"
LangString MSG_PERUSER_CONFIG ${LANG_TRADCHINESE}      "你要讓不同使用者使用各自的設定值嗎？$\n$\n(若選「否」，所有使用者會共用程式安裝目錄內的設定檔)"
LangString MSG_PERUSER_CONFIG ${LANG_ENGLISH}          "Do you want to enable per-user config files?"
LangString MSG_UNINSTALLED ${LANG_TRADCHINESE}         "$(^Name) 已成功地從你的電腦移除。"
LangString MSG_UNINSTALLED ${LANG_ENGLISH}             "$(^Name) has been removed sucessfully."
LangString MSG_REMOVE_PROGRAM ${LANG_TRADCHINESE}      "你確定要完全移除 $(^Name) ，其及所有的元件？"
LangString MSG_REMOVE_PROGRAM ${LANG_ENGLISH}          "Are you sure you want to completely remove $(^Name) and all of its components?"
LangString MSG_REMOVE_CONFIG ${LANG_TRADCHINESE}       "是否連設定檔案一起刪除？"
LangString MSG_REMOVE_CONFIG ${LANG_ENGLISH}           "Do you want to delete config files?"
LangString MSG_KEEP_CONFIG ${LANG_TRADCHINESE}         "保留原設定檔？ (可能不相容)"
LangString MSG_KEEP_CONFIG ${LANG_ENGLISH}             "Do you want to keep old configuration? It may not be compatible with current version."
LangString MSG_OPEN_BACKUP_FILES ${LANG_TRADCHINESE}   "開啟備份的設定？"
LangString MSG_OPEN_BACKUP_FILES ${LANG_ENGLISH}       "Open backup configuration files?"

; MUI end ------

Function .onInit
  !insertmacro MUI_LANGDLL_DISPLAY
  ; MessageBox MB_ICONEXCLAMATION|MB_OK "${BUILD_TIME}"
FunctionEnd


Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
InstallDir "$PROGRAMFILES\${PRODUCT_DIR}"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section SEC01

  SetOutPath "$INSTDIR"
  
  SetOverwrite on
  ;SetOverwrite ifnewer
  File /r /x "Symbols.txt" /x "Config" /x "*.svn" "${SRC_DIR}\*.*"
  ;File "Migrate\Release\Migrate.exe"

  SetOverwrite off
  File "${SRC_DIR}\Symbols.txt"

  SetOutPath "$INSTDIR\Config"
  File "${SRC_DIR}\Config\Config.ini"
  File "${SRC_DIR}\Config\BBSFavorites"
  File "${SRC_DIR}\Config\FUS"

  ; Backup icons and UI file
  StrCpy $clean_install 1
  IfFileExists $INSTDIR\Config\UI 0 cleaninstall
  StrCpy $clean_install 0
  CreateDirectory $INSTDIR\Config\backup
  CopyFiles $INSTDIR\Config\*.bmp $INSTDIR\Config\backup
  CopyFiles $INSTDIR\Config\UI $INSTDIR\Config\backup
cleaninstall:

  SetOverwrite on
  File "${SRC_DIR}\Config\*.bmp"
  File "${SRC_DIR}\Config\UI"

  SetShellVarContext all    ; Install for all users

  CreateDirectory "$SMPROGRAMS\${PRODUCT_DIR}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_DIR}\${PRODUCT_NAME} ${PRODUCT_VERSION}.lnk" "$INSTDIR\PCMan.exe"
  CreateShortCut "$DESKTOP\${PRODUCT_NAME} ${PRODUCT_VERSION}.lnk" "$INSTDIR\PCMan.exe"

  CreateShortCut "$SMPROGRAMS\${PRODUCT_DIR}\$(SYMBOLS_LNK)" "$INSTDIR\Symbols.exe" "$INSTDIR\Symbols.exe"

  ; Check if we are under Windows NT
  ClearErrors
  ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows NT\CurrentVersion" CurrentVersion
  IfErrors Finish 0

  ;
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "$(MSG_PERUSER_CONFIG)" IDYES UsePerUser
  ; Use Global Settings (Portable)
  ;; Create an empty `Portable' file to tell PCMan that to use global settings
  Rename "$INSTDIR\_Portable" "$INSTDIR\Portable"
  Goto EndOfPerUser
  
  ; Now, always use per-user configuration for instaionll.
  ; If the user wants to use global configuration,
  ; he/she should download protable version,
  ; Because Program Files is not writable for users.
  ; it doesn't make sense that 
UsePerUser:
  Delete "$INSTDIR\_Portable"
  ReadRegStr $0 HKCU "${SHELLFOLDERS}" "AppData"
  StrCmp $0 "" 0 +4
  ReadRegStr $0 HKLM "${SHELLFOLDERS}" "Common AppData"
  StrCmp $0 "" 0 +2
  StrCpy $0 "$WINDIR\Application Data"
  ; Use Peronsal Settings
EndOfPerUser:

  ;; Delete the previous UI file
  ; Delete "$0\${CONFIG_FOLDER}\UI"
   
!ifdef _COMBO_
  ; Setting WebControl Compatible key.
  ReadRegDWORD $0 HKLM "SOFTWARE\Microsoft\Internet Explorer\MAIN\FeatureControl\FEATURE_BROWSER_EMULATION" "pcman.exe"
  StrCmp $0 "" 0 Finish
  ClearErrors
  WriteRegDWORD HKLM "SOFTWARE\Microsoft\Internet Explorer\MAIN\FeatureControl\FEATURE_BROWSER_EMULATION" "pcman.exe" 8000
  DetailPrint "Writing register key for webcontrol."
  IfErrors Finish 0
!endif
  
Finish:


SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_DIR}\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_DIR}\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\PCMan.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\PCMan.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(MSG_UNINSTALLED)"
FunctionEnd

Function un.onInit
!insertmacro MUI_UNGETLANGUAGE
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "$(MSG_REMOVE_PROGRAM)" IDYES YES
  Abort
  YES:
FunctionEnd

Section Uninstall
  RMDir /r "$INSTDIR\Conv"
  RMDir /r "$INSTDIR\Keyboard"
  RMDir /r "$INSTDIR\SearchPlugins"  ; Should search plugins be preserved?
  Delete "$INSTDIR\PCMan.exe"
  Delete "$INSTDIR\PCMan.exe.manifest"
  Delete "$INSTDIR\B2U"
  Delete "$INSTDIR\U2B"
  Delete "$INSTDIR\Story.txt"
  Delete "$INSTDIR\changelog.txt"
  Delete "$INSTDIR\BBSList"
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\Symbols.exe"
  Delete "$INSTDIR\uninst.exe"

  ; IfFileExists "$INSTDIR\Portable"  0 +5
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "$(MSG_REMOVE_CONFIG)" IDNO keep_config
  RMDir /r "$INSTDIR\Config"
  RMDir /r "$INSTDIR\backup"
  Delete "$INSTDIR\Symbols.txt"
  keep_config:
  Delete "$INSTDIR\Portable"

  SetShellVarContext all    ; Install for all users

  Delete "$SMPROGRAMS\${PRODUCT_DIR}\${PRODUCT_NAME} ${PRODUCT_VERSION}.lnk"
  RMDir /r "$SMPROGRAMS\${PRODUCT_DIR}"

  Delete "$DESKTOP\${PRODUCT_NAME} ${PRODUCT_VERSION}.lnk"

  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd

